mapscripts EvergreenTown_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: EvergreenTown_OnTransition
}

script EvergreenTown_OnTransition
{
    if(!(flag(FLAG_RIVAL_VISIBILITY)))
    {
        setflag(FLAG_RIVAL_VISIBILITY)
    }
}

// Event Scripts
script EvergreenTown_EventScript_EvergreenJacksonBattle
{
    msgbox(EvergreenTown_TextScript_RivalIntro)
    setflag(FLAG_NO_WHITEOUT)
    while(var(VAR_RESULT) != B_OUTCOME_WON)
    {
        if(flag(FLAG_STARTER_BALL_ONE))
        {
            trainerbattle_no_intro(TRAINER_RIVAL_EVERGREEN_IMPIDIMP, EvergreenTown_TextScript_RivalBattleFinished)
            specialvar(VAR_RESULT, GetBattleOutcome)
        }
        elif(flag(FLAG_STARTER_BALL_TWO))
        {
            trainerbattle_no_intro(TRAINER_RIVAL_EVERGREEN_BELDUM, EvergreenTown_TextScript_RivalBattleFinished)
            specialvar(VAR_RESULT, GetBattleOutcome)
        }
        elif(flag(FLAG_STARTER_BALL_THREE))
        {
            trainerbattle_no_intro(TRAINER_RIVAL_EVERGREEN_DUSKULL, EvergreenTown_TextScript_RivalBattleFinished)
            specialvar(VAR_RESULT, GetBattleOutcome)
        }

        if(var(VAR_RESULT) == B_OUTCOME_LOST)
        {
            msgbox(format("Jackson: That was a rush, you did really well!\pOur next battle will be even more amazing, I can tell.\pBe safe bro and see you again soon!"))
            closemessage
            applymovement(12, EvergreenTown_MovementScript_RivalAway)
            waitmovement(0)
            removeobject(12)
            special(HealPlayerParty)
            setvar(VAR_RIVAL_STATE, 2)
            setflag(FLAG_RIVAL_HOUSE_VISIBILITY)
            setflag(FLAG_HEAL_MACHINE_USABLE)
            clearflag(FLAG_NO_WHITEOUT)
            release
            end
        }
    }
    special(HealPlayerParty)
    setflag(FLAG_RIVAL_HOUSE_VISIBILITY)
    setflag(FLAG_HEAL_MACHINE_USABLE)
    clearflag(FLAG_NO_WHITEOUT)
    call(EvergreenTown_EventScript_EvergreenJacksonBeaten)
}

script EvergreenTown_EventScript_EvergreenJacksonBeaten
{
    msgbox(format("Jackson: You're really good at this. It was a blast to fight you!\pI'll make sure to get better and improve my team then challenge you again.\pBe safe bro."))
    closemessage
    applymovement(12, EvergreenTown_MovementScript_RivalAway)
    waitmovement(0)
    removeobject(12)
    setvar(VAR_RIVAL_STATE, 2)
    release
    end
}

// NPC Scripts
script EvergreenTown_NPCScript_OldMan
{
    lock
    faceplayer
    msgbox(format("Hello there, {PLAYER}! I can never get enough of how beautiful this town is.\pGood luck on your future endeavors!"))
    release
    end
}

script EvergreenTown_NPCScript_Beauty
{
    lock
    faceplayer
    msgbox(format("These flowers here are so pretty. I want to grow ones like these at my home."))
    release
    end
}

script EvergreenTown_NPCScript_Boy
{
    lock
    faceplayer
    if(!flag(FLAG_RECEIVED_EVERGREEN_POTION))
    {
        msgbox(format("I'm trying to surprise this girl over on the other side of town with these flowers I found, will you go away?"), MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES)
        {
            msgbox(format("Thanks.\pHere's a potion for your troubles..."))
            giveitem(ITEM_POTION)
            setflag(FLAG_RECEIVED_EVERGREEN_POTION)
            applymovement(7, EvergreenTown_MovementScript_Boy)
            waitmovement(0)
        }
        else
        {
            msgbox(format("Ugh!\pTake this and leave..."))
            giveitem(ITEM_POTION)
            setflag(FLAG_RECEIVED_EVERGREEN_POTION)
            applymovement(7, EvergreenTown_MovementScript_Boy)
            waitmovement(0)
        }
    }
    else
    {
        msgbox("Stop bothering me.")
        applymovement(7, EvergreenTown_MovementScript_Boy)
        waitmovement(0)
    }
    release
    end
}

script EvergreenTown_NPCScript_Fisherman
{
    lock
    faceplayer
    msgbox(format("Fishing here is so calming. I love coming here with my best buddy Wingull!"))
    applymovement(4, EvergreenTown_MovementScript_Wingull)
    waitmovement(0)
    playmoncry(SPECIES_WINGULL, CRY_MODE_NORMAL)
    waitmoncry
    applymovement(3, EvergreenTown_MovementScript_Fisherman)
    waitmovement(0)
    msgbox(format("I'm glad to hear you agree buddy!"))
    applymovement(4, EvergreenTown_MovementScript_Wingull2)
    waitmovement(0)
    applymovement(3, EvergreenTown_MovementScript_Fisherman2)
    waitmovement(0)
    msgbox(format("Isn't he the coolest?"))
    applymovement(3, EvergreenTown_MovementScript_Fisherman3)
    waitmovement(0)
    release
    end
}

// Trigger Scripts
script EvergreenTown_TriggerScript_StampedeBlock
{
    lock
    msgbox(format("I can't let you pass right now. There's been a stampede that's been wreaking havoc here. Once it's been routed, people can go through again."))
    closemessage
    applymovement(LOCALID_PLAYER, EvergreenTown_MovementScript_Player)
    waitmovement(0)
    release
    end
}

script EvergreenTown_TriggerScript_StampedeBlock2
{
    lock
    applymovement(8, EvergreenTown_MovementScript_AceTrainer)
    waitmovement(0)
    msgbox(format("I can't let you pass right now. There's been a stampede that's been wreaking havoc here. Once it's been routed, people can go through again."))
    closemessage
    applymovement(LOCALID_PLAYER, EvergreenTown_MovementScript_Player)
    waitmovement(0)
    applymovement(8, EvergreenTown_MovementScript_AceTrainer2)
    waitmovement(0)
    release
    end
}

script EvergreenTown_TriggerScript_GotoProfessor
{
    lock
    msgbox(format("The Professor asked for you. Now's not the time to be leaving the town."))
    closemessage
    applymovement(LOCALID_PLAYER, EvergreenTown_MovementScript_GotoProfessor)
    waitmovement(0)
    release
    end
}

script EvergreenTown_TriggerScript_RivalMeet
{
    lock
    opendoor(16, 14)
    waitdooranim
    addobject(11)
    applymovement(11, EvergreenTown_MovementScript_RivalOutDoor)
    waitmovement(0)
    closedoor(16, 14)
    waitdooranim
    applymovement(11, EvergreenTown_MovementScript_RivalOut)
    waitmovement(0)
    msgbox(format("Jackson: Yo, {PLAYER}! I heard that you're going to be doing something for the professor. I recently just got my starter and plan on fighting the gyms to qualify for the league challenge, so let's fight at the edge of town when you're done at the lab.\pWait for me right before route 1 and I'll come to you.\pSee ya!"))
    closemessage
    applymovement(11, EvergreenTown_MovementScript_RivalIn)
    waitmovement(0)
    opendoor(16, 14)
    waitdooranim
    applymovement(11, EvergreenTown_MovementScript_RivalInDoor)
    waitmovement(0)
    removeobject(11)
    closedoor(16, 14)
    waitdooranim
    setvar(VAR_RIVAL_STATE, 1)
    release
    end
}

script EvergreenTown_TriggerScript_RivalMeet2
{
    lock
    opendoor(16, 14)
    waitdooranim
    addobject(11)
    applymovement(11, EvergreenTown_MovementScript_RivalOutDoor)
    waitmovement(0)
    closedoor(16, 14)
    waitdooranim
    applymovement(11, EvergreenTown_MovementScript_RivalOut2)
    waitmovement(0)
    msgbox(format("Jackson: Yo, {PLAYER}! I heard that you're going to be doing something for the professor. I recently just got my starter and plan on fighting the gyms to qualify for the league challenge, so let's fight at the edge of town when you're done at the lab.\pWait for me right before route 1 and I'll come to you.\pSee ya!"))
    closemessage
    applymovement(11, EvergreenTown_MovementScript_RivalIn2)
    waitmovement(0)
    opendoor(16, 14)
    waitdooranim
    applymovement(11, EvergreenTown_MovementScript_RivalInDoor)
    waitmovement(0)
    removeobject(11)
    closedoor(16, 14)
    waitdooranim
    setvar(VAR_RIVAL_STATE, 1)
    release
    end
}

script EvergreenTown_TriggerScript_RivalMeet3
{
    lock
    opendoor(16, 14)
    waitdooranim
    addobject(11)
    applymovement(11, EvergreenTown_MovementScript_RivalOutDoor)
    waitmovement(0)
    closedoor(16, 14)
    waitdooranim
    applymovement(11, EvergreenTown_MovementScript_RivalOut3)
    waitmovement(0)
    msgbox(format("Jackson: Yo, {PLAYER}! I heard that you're going to be doing something for the professor. I recently just got my starter and plan on fighting the gyms to qualify for the league challenge, so let's fight at the edge of town when you're done at the lab.\pWait for me right before route 1 and I'll come to you.\pSee ya!"))
    closemessage
    applymovement(11, EvergreenTown_MovementScript_RivalIn3)
    waitmovement(0)
    opendoor(16, 14)
    waitdooranim
    applymovement(11, EvergreenTown_MovementScript_RivalInDoor)
    waitmovement(0)
    removeobject(11)
    closedoor(16, 14)
    waitdooranim
    setvar(VAR_RIVAL_STATE, 1)
    release
    end
}

script EvergreenTown_TriggerScript_FirstRivalBattle
{
    lock
    addobject(12)
    applymovement(12, EvergreenTown_MovementScript_ToMC)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, EvergreenTown_MovementScript_FaceRival)
    waitmovement(0)
    call(EvergreenTown_EventScript_EvergreenJacksonBattle)
}

script EvergreenTown_TriggerScript_FirstRivalBattle2
{
    lock
    addobject(12)
    applymovement(12, EvergreenTown_MovementScript_ToMC2)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, EvergreenTown_MovementScript_FaceRival)
    waitmovement(0)
    call(EvergreenTown_EventScript_EvergreenJacksonBattle)
}

// Sign Scripts
script EvergreenTown_SignScript_PlayerHouse
{
    lock
    msgbox(format("{PLAYER}'s House..."))
    release
    end
}

script EvergreenTown_SignScript_RivalHouse
{
    lock
    msgbox(format("Rival's House..."))
    release
    end
}

script EvergreenTown_SignScript_ProfessorsLab
{
    lock
    msgbox(format("Professor Sequioa's Lab..."))
    release
    end
}

script EvergreenTown_SignScript_EvergreenSign
{
    lock
    msgbox(format("Evergreen Town, a lush homey atmosphere."))
    release
    end
}

// Movement Scripts
movement EvergreenTown_MovementScript_Wingull
{
    face_right
}

movement EvergreenTown_MovementScript_Wingull2
{
    face_down
}

movement EvergreenTown_MovementScript_Fisherman
{
    face_left
}

movement EvergreenTown_MovementScript_Fisherman2
{
    face_right
}

movement EvergreenTown_MovementScript_Fisherman3
{
    face_down
}

movement EvergreenTown_MovementScript_Boy
{
    face_left
}

movement EvergreenTown_MovementScript_Player
{
    walk_left
}

movement EvergreenTown_MovementScript_Player2
{
    walk_left * 2
}

movement EvergreenTown_MovementScript_AceTrainer
{
    walk_down
    face_left
}

movement EvergreenTown_MovementScript_AceTrainer2
{
    walk_up
    face_left
}

movement EvergreenTown_MovementScript_GotoProfessor
{
    walk_up
}

movement EvergreenTown_MovementScript_RivalOutDoor
{
    walk_down
}

movement EvergreenTown_MovementScript_RivalInDoor
{
    walk_up
}

movement EvergreenTown_MovementScript_RivalOut
{
    face_left
}

movement EvergreenTown_MovementScript_RivalOut2
{
    walk_down
    face_left
}

movement EvergreenTown_MovementScript_RivalOut3
{
    walk_down * 2
    face_left
}

movement EvergreenTown_MovementScript_RivalIn
{
    face_up
}

movement EvergreenTown_MovementScript_RivalIn2
{
    walk_up
}

movement EvergreenTown_MovementScript_RivalIn3
{
    walk_up * 2
}

movement EvergreenTown_MovementScript_ToMC
{
    walk_down * 5
}

movement EvergreenTown_MovementScript_ToMC2
{
    walk_right
    walk_down * 5
}

movement EvergreenTown_MovementScript_FaceRival
{
    face_up
}

movement EvergreenTown_MovementScript_RivalAway
{
    walk_up * 5
}

// Text Scripts
text EvergreenTown_TextScript_RivalIntro
{
    format("Jackson: Hey {PLAYER}, are you ready to have our battle? I've wanted to do this for a long time!")
}

text EvergreenTown_TextScript_RivalBattleFinished
{
    "Good game, that was fun!"
}
